/*
 * generated by Xtext 2.13.0-SNAPSHOT
 */
package io.typefox.yang.validation

import com.google.inject.Inject
import com.google.inject.Singleton
import io.typefox.yang.validation.SubstatementValidationHelper.MultiStatus
import io.typefox.yang.yang.Import
import io.typefox.yang.yang.Module
import io.typefox.yang.yang.Revision
import io.typefox.yang.yang.Statement
import io.typefox.yang.yang.YangVersion
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EStructuralFeature
import org.eclipse.xtext.validation.Check

import static io.typefox.yang.validation.IssueCodes.*
import static io.typefox.yang.yang.YangPackage.Literals.*

/**
 * This class contains custom validation rules for the YANG language. 
 */
@Singleton
class YangValidator extends AbstractYangValidator {

	@Inject
	SubstatementRuleProvider substatementRuleProvider;

	@Check
	def void checkVersion(YangVersion it) {
		if (yangVersion != "1.1") {
			error("The version must be '1.1'.", it, YANG_VERSION__YANG_VERSION, INCORRECT_VERSION);
		}
	}

	@Check
	def void checkModuleCardinalities(Module module) {
		checkCardinalities(module, [module -> ABSTRACT_MODULE__NAME]);
	}

	@Check
	def void checkImportCardinalities(Import _import) {
		checkCardinalities(_import, [_import -> ABSTRACT_IMPORT__MODULE]);
	}

	@Check
	def void checkImportCardinalities(Revision revision) {
		checkCardinalities(revision, [revision -> REVISION__REVISION]);
	}

	private def <S extends Statement> checkCardinalities(S container,
		(S)=>Pair<? extends EObject, ? extends EStructuralFeature> issueLocationProvider) {

		val rule = substatementRuleProvider.get(container.eClass);
		if (rule === null) {
			return;
		}
	
		val globalStatus = rule.checkContext(container);
		if (!globalStatus.OK) {
			if (globalStatus instanceof MultiStatus) {
				val issueLocation = issueLocationProvider.apply(container);
				globalStatus.children.forEach[
					error(message, issueLocation.key, issueLocation.value, issueCode);
				];
			}
		}		

		container.subStatements.forEach[statement, index |
			val status = rule.checkStatementInContext(statement, container);
			if (!status.OK) {
				error(status.message, container, STATEMENT__SUB_STATEMENTS, index, status.issueCode);
			}
		];
	}

}
